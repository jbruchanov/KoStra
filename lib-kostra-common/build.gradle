plugins {
    id("org.jetbrains.kotlin.multiplatform")
    id("com.android.library")
}

apply from: "$rootProject.projectDir/build-ktlint.gradle"
apply from: "$rootProject.projectDir/build-publish.gradle"
apply from: "$rootProject.projectDir/build-android-lib.gradle"
apply from: "$rootProject.projectDir/build-native-lib.gradle"

project.group = libs.versions.project.group.get()
project.version = libs.versions.project.version.get()

kotlin {
    androidTarget {
        publishLibraryVariants("release")
    }

    jvm {
        jvmToolchain(libs.versions.jvmtarget.get().toInteger())
    }

    createNativeTarget(delegate)

    iosX64()
    iosArm64()
    iosSimulatorArm64()

    sourceSets {
        commonMain {
            dependencies {
            }
        }
        commonTest {
            dependencies {
                implementation(kotlin("test"))
            }
        }

        iosX64Main
        iosArm64Main
        iosSimulatorArm64Main

        iosMain {
            dependsOn(commonMain)
            iosX64Main.dependsOn(delegate)
            iosArm64Main.dependsOn(delegate)
            iosSimulatorArm64Main.dependsOn(delegate)
        }

        iosX64Test
        iosArm64Test
        iosSimulatorArm64Test
        iosTest {
            dependsOn(commonTest)
            iosX64Test.dependsOn(delegate)
            iosArm64Test.dependsOn(delegate)
            iosSimulatorArm64Test.dependsOn(delegate)
        }

        jvmTest {
            dependencies {
                implementation(libs.bundles.unittest.jvm)
            }
        }

        nativeMain
    }
}

android {
    namespace = "com.jibru.kostra"
}
