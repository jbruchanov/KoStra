pluginManagement {
    repositories {
        mavenLocal()
        maven { url "https://maven.pkg.jetbrains.space/public/p/compose/dev" }
        gradlePluginPortal()
        google()
    }
}

plugins {
    //no toml here yet
    id("de.fayard.refreshVersions") version "0.51.0"
}

refreshVersions {
    //workaround to avoid having empty versions.properties generated in root
    file("build/").mkdirs()
    versionsPropertiesFile = file("build/versions.properties")
}

dependencyResolutionManagement {
    repositories {
        mavenLocal()
        maven { url "https://maven.pkg.jetbrains.space/public/p/compose/dev" }
        google()
        mavenCentral()
    }
}

rootProject.name = "kostra"
include("kostra-lib")

def incBuild = false
if (incBuild) {
    ['appJvm', 'shared'].forEach { dirName ->
        include(":$dirName")
        project(":$dirName").projectDir = file("sample/$dirName")
    }
    includeBuild("kostra-gradle-plugin")
} else {
    include("kostra-gradle-plugin")
}
