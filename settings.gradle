pluginManagement {
    repositories {
        mavenLocal()
        maven { url "https://maven.pkg.jetbrains.space/public/p/compose/dev" }
        gradlePluginPortal()
        google()
    }
}

plugins {
    //no toml here yet
    id("de.fayard.refreshVersions") version "0.60.3"
}

refreshVersions {
    //workaround to avoid having empty versions.properties generated in root
    file("build/").mkdirs()
    versionsPropertiesFile = file("build/versions.properties")
}

dependencyResolutionManagement {
    repositories {
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri(githubPackagesUrl)
        }
        maven { url "https://maven.pkg.jetbrains.space/public/p/compose/dev" }
        google()
        mavenCentral()
    }
}

//for testing the app-sample, open the sample as root of the project

rootProject.name = "kostra"
include("kostra-gradle-plugin")
//to avoid having lib- as prefix for each KMP variant, artifact is changing just main one
["kostra-common", "kostra-compose"].forEach {
    include(it)
    project(":$it").projectDir = file("lib-$it")
}
